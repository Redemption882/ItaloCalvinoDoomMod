ACTOR ShotgunClip : Ammo
{
   +INVENTORY.IGNORESKILL
   Inventory.MaxAmount 8
}

ACTOR PumpShotgun : Weapon
{
   Inventory.PickupMessage "You Got the Shotgun!"
   Inventory.PickupSound "weapon/pickup"
   Weapon.AmmoType "ShotgunClip"
   Weapon.AmmoGive 0
   Weapon.AmmoUse 1
   Weapon.AmmoType2 "Shell"
   Weapon.AmmoGive2 10
   Weapon.Kickback 80
   Weapon.BobStyle InverseSmooth
   Weapon.BobSpeed 2.2
   Weapon.BobRangeY 0.3
   Weapon.BobRangeX 0.2
   Weapon.SelectionOrder 1303
   Weapon.SlotNumber 3
   AttackSound "pumpshotgun/fire"
   Scale 1.2
   +NOAUTOAIM
   +NOAUTOFIRE
   +Weapon.Ammo_Optional 
   +INVENTORY.IGNORESKILL
   +weapon.noalert
   States
   {
   Ready:
     STGD A 1 A_WeaponReady(WRF_ALLOWRELOAD)
     Goto Ready
   Deselect:
     TNT1 A 0 
     Goto DeselectAnim
   DeselectAnim:	
     TNT1 A 0 A_PlaySound ("weapon/inventory")
     STGI ABCDEFG 1
     Goto DeselectEnd
   DeselectEnd:
     TNT1 AAAAAAAAAAAAAA 00 A_Lower
     Loop
   Select:
     TNT1 A 1 
     TNT1 AAAAAAAAAAAAAA 00 A_Raise
     Goto SelectAnim	 
   SelectAnim:
     TNT1 A 0 A_PlaySound ("weapon/inventory")
     STGI GFEDCBA 1
     Goto Ready
   Fire:
      TNT1 A 0 A_JumpIfNoAmmo("NoAmmo")
      STGF A 1 
      STGF B 1 A_FireBullets (4,3,8,8,"BulletSpark")
      TNT1 A 0 A_SetPitch(pitch-2.0)
      TNT1 A 0 A_SetAngle(angle+0.6)
      TNT1 A 0 BRIGHT A_GunFlash
      TNT1 A 0 A_AlertMonsters
	  STGF CD 1
	  STGD A 5
	  STGR ABCDEF 1
	  STGR G 3
	  TNT1 A 0 A_PlaySound ("pumpshotgun/cockin")
	  STGR HIJ 1
      TNT1 A 0 A_FireCustomMissile("ShotgunCasingSpawner",55,0,8,0)
	  STGR J 8
	  TNT1 A 0 A_PlaySound ("pumpshotgun/cockout")
	  STGR IH 1
	  STGR G 5
	  STGR FEDCBA 1
	  STGD A 1
      TNT1 A 0 A_Refire
      Goto Ready
   Reload:
      TNT1 A 0 A_JumpIfInventory("ShotgunClip", 8, 2)
      TNT1 A 0 A_JumpIfInventory("Shell", 1, "ReloadWork")
      TNT1 A 0
      Goto Ready
   ReloadWork:
      STGR ABC 1
	  STGR D 5
   ReloadLoop:
	  TNT1 A 0 A_PlaySound ("pumpshotgun/shellin")
	  STGS AB 1
	  TNT1 A 0 A_TakeInventory("Shell", 1)
	  TNT1 A 0 A_GiveInventory("ShotgunClip",1)
	  STGS A 1
	  STGR D 5
      TNT1 A 0 A_JumpIfInventory("ShotgunClip", 8, "ReloadEnd")
      TNT1 A 0 A_JumpIfInventory("Shell", 1, "ReloadLoop")
      Goto ReloadEnd
   ReloadEnd: 
      STGR D 5
	  STGR EF 1
	  STGR G 3
	  TNT1 A 0 A_PlaySound ("pumpshotgun/cockin")
	  STGR HIJ 1
	  STGR J 8
	  TNT1 A 0 A_PlaySound ("pumpshotgun/cockout")
	  STGR IH 1
	  STGR G 5
	  STGR FEDCBA 1
	  Goto Full
    Full:
      STGD A 0
      Goto Ready 
    NoAmmo:
      TNT1 A 0 A_JumpIfInventory("ShotgunClip", 8, 2)
      TNT1 A 0 A_JumpIfInventory("Shell", 1, "ReloadWork")
      TNT1 A 0 A_PlaySound ("weapon/noammo")
    Goto Ready 
   Flash:
      TNT1 A 4 Bright A_Light1
      TNT1 A 3 Bright A_Light2
      Goto LightDone
   Spawn:
      SHOT A -1
      Stop
   }
}
