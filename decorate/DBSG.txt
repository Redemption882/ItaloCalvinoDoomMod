ACTOR DBSGClip : Ammo
{
   +INVENTORY.IGNORESKILL
   Inventory.MaxAmount 2
}

actor DBSG : Weapon Replaces SuperShotgun
{
  inventory.pickupmessage "You got the Double-barreled Shotgun!"
  Inventory.PickupSound "weapon/pickup"
  Weapon.SelectionOrder 1303
  Weapon.AmmoType1 "DBSGClip"
  Weapon.AmmoGive1 0
  Weapon.AmmoUse1 1
  Weapon.AmmoType2 "Shell"
  Weapon.AmmoGive2 10
  Weapon.kickback 120
  Weapon.SlotNumber 3
  Weapon.BobStyle InverseSmooth
  Weapon.BobSpeed 2.2
  Weapon.BobRangeY 0.5
  Weapon.BobRangeX 0.5
  Scale 0.1
  +NOAUTOFIRE
  +NOAUTOAIM
  +INVENTORY.IGNORESKILL
  +Weapon.Ammo_Optional 
  +Weapon.Alt_Ammo_Optional
  +WEAPON.ALT_USES_BOTH
  AttackSound "dbsg/fire"
  States
  {
  Ready:
    SSID A 1 A_WeaponReady(WRF_ALLOWRELOAD)
	Loop
   Deselect:
	 TNT1 A 0 
     Goto DeselectAnim
    DeselectAnim:
	 TNT1 A 0 A_PlaySound ("weapon/inventory")
	 SSIV ABCDEFG 1
	 Goto DeselectEnd
   DeselectEnd:
	 TNT1 AAAAAAAAAAAAAA 00 A_Lower
     Loop
   Select:
	 TNT1 A 1 
   	 TNT1 AAAAAAAAAAAAAA 00 A_Raise
     Goto SelectAnim	 
   SelectAnim:
	 TNT1 A 0 A_PlaySound ("weapon/inventory")
	 SSIV GFEDCBA 1
	 SSID A 1
	 Goto Ready
   Fire:
    TNT1 A 0 A_JumpIfInventory ("DBSGFire2", 1, "AltReloadWork")
    TNT1 A 0 A_JumpIfNoAmmo("NoAmmo")
  	TNT1 A 0 BRIGHT A_GunFlash
	TNT1 A 0 A_AlertMonsters
    SSIF A 1 
	TNT1 A 0 BRIGHT A_FireBullets(8,6,6,7,"BulletSpark")
	TNT1 A 0 BRIGHT A_FireBullets(8,6,6,7,"BulletSpark")
	TNT1 A 0 A_SetPitch(pitch-2.0)
	TNT1 A 0 A_SetAngle(angle+0.6)
	SSIF BC 1
	SSID A 6
    Goto Ready	
   AltFire:
    TNT1 A 0 A_JumpIfInventory ("DBSGFire2", 1, "AltFire2")
    TNT1 A 0 A_JumpIfNoAmmo("NoAmmo")
	TNT1 A 0 A_GiveInventory ("DBSGFire2")
  	TNT1 A 0 BRIGHT A_GunFlash
	TNT1 A 0 A_AlertMonsters
    SSIF E 1 BRIGHT A_FireBullets(6,4,6,7,"BulletSpark")
	TNT1 A 0 A_SetPitch(pitch-2.0)
	TNT1 A 0 A_SetAngle(angle+0.6)
	SSIF BC 1
	SSID A 6
    Goto Ready
   AltFire2:
    TNT1 A 0 A_JumpIfNoAmmo("NoAmmo")
  	TNT1 A 0 BRIGHT A_GunFlash
	TNT1 A 0 A_TakeInventory ("DBSGFire2")
	TNT1 A 0 A_AlertMonsters
    SSIF D 1 BRIGHT A_FireBullets(6,4,6,7,"BulletSpark")
	TNT1 A 0 A_SetPitch(pitch-2.0)
	TNT1 A 0 A_SetAngle(angle+0.2)
	SSIF BC 1
	SSID A 6
    Goto Ready
   Reload:
	TNT1 A 0 A_JumpIfInventory("DBSGClip", 2, 2)
	TNT1 A 0 A_JumpIfInventory("Shell", 1, "ReloadWork")
	TNT1 A 0
	Goto Ready
   ReloadWork:
    TNT1 A 0 A_JumpIfInventory("DBSGFire2", 1, "AltReloadWork")
    SSID A 1
	SSIR A 1
	TNT1 A 0 A_PlaySound ("dbsg/open")
	SSIR BC 1
	SSIL A 1
	SSIR DE 1
	TNT1 A 0 A_FireCustomMissile("ShotgunCasingSpawner",0,0,0,0)
	TNT1 A 0 A_FireCustomMissile("ShotgunCasingSpawner",0,0,0,0)
	SSIR E 7
	SSIR FGHI 1
	TNT1 A 0 A_PlaySound ("dbsg/reload")
	SSIR J 1
	SSIA AB 1
	SSIR KLMN 1
	SSIR N 7
	TNT1 A 0 A_PlaySound ("dbsg/close")
	SSIR O 1
	SSIL B 1
	SSIR PBA 1 
	Goto ReloadLoop
   AltReloadWork:
    TNT1 A 0 A_TakeInventory ("DBSGFire2")
    SSID A 1
	SSIR A 1
	TNT1 A 0 A_PlaySound ("dbsg/open")
	SSIR B 1
	SSGR ABC 1
	TNT1 A 0 A_FireCustomMissile("ShotgunCasingSpawner",0,0,0,0)
	SSGR E 7
	SSGR FGHI 1
	TNT1 A 0 A_PlaySound ("dbsg/reload")
	SSIR J 1
	SSIA AB 1
	SSIR KLMN 1
	SSIR N 7
	TNT1 A 0 A_PlaySound ("dbsg/close")
	SSIR O 1
	SSIL B 1
	SSIR PBA 1 
	Goto ReloadLoop
   ReloadLoop:
	TNT1 A 0 A_TakeInventory("Shell", 1)
	TNT1 A 0 A_GiveInventory("DBSGClip",1)
	TNT1 A 0 A_JumpIfInventory("DBSGClip", 2, "Full")
	TNT1 A 0 A_JumpIfInventory("Shell", 1, "ReloadLoop")
	Goto Full
   Full:
    SSID A 0
    Goto Ready 
   NoAmmo:
    TNT1 A 0 A_JumpIfInventory("DBSGClip", 2, 2)
	TNT1 A 0 A_JumpIfInventory("Shell", 1, "ReloadWork")
	TNT1 A 0 A_PlaySound ("weapon/noammo")
	TNT1 A 0 A_TakeInventory ("DBSGFire2")
    Goto Ready 
  Flash:
    TNT1 A 4 Bright A_Light1
    TNT1 A 3 Bright A_Light2
    Goto LightDone
  Spawn:
    SSGN A -1
    Stop
  }
}
