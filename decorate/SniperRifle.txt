ACTOR SniperRifleClip : Ammo
{
   +INVENTORY.IGNORESKILL
   Inventory.MaxAmount 10
}

ACTOR SniperRifle : Weapon
{
   inventory.pickupmessage "You got the Sniper Rifle!"
   Inventory.PickupSound "weapon/pickup"
   Weapon.AmmoType1 "SniperRifleClip"
   Weapon.AmmoGive1 0
   Weapon.AmmoUse1 1
   Weapon.AmmoType2 "Clip"
   Weapon.Kickback 40
   Weapon.BobStyle InverseSmooth
   Weapon.BobSpeed 2.2
   Weapon.BobRangeY 0.3
   Weapon.BobRangeX 0.2
   Weapon.SelectionOrder 1303
   Weapon.SlotNumber 6
   AttackSound "sniper/fire"
   Scale 0.7
   +NOAUTOFIRE
   +NOAUTOAIM
   +INVENTORY.IGNORESKILL
   +Weapon.Ammo_Optional 
   +weapon.wimpy_weapon
   +weapon.noalert
   States
   {
   Ready:
     TNT1 A 0 A_JumpIfInventory("ScopeZoom", 1, "ScopedReady")
     SNPG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
     Goto Ready
   ScopedReady:
     SNPS A 1 A_WeaponReady(WRF_ALLOWRELOAD)
     Goto Ready
   Deselect:
	 TNT1 A 0 
     Goto DeselectAnim
    DeselectAnim:
	 TNT1 A 0 A_PlaySound ("weapon/inventory")
	 SNPI ABCDEFGHIJ 1
	 Goto DeselectEnd
   DeselectEnd:
	 TNT1 AAAAAAAAAAAAAA 00 A_Lower
     Loop
   Select:
	 TNT1 A 1 
   	 TNT1 AAAAAAAAAAAAAA 00 A_Raise
     Goto SelectAnim	 
   SelectAnim:
	 TNT1 A 0 A_PlaySound ("weapon/inventory")
	 SNPI JIHGFEDCBA 1
	 Goto Ready
   Fire:
     TNT1 A 0 A_JumpIfInventory("ScopeZoom", 1, "ScopedFire")
     TNT1 A 0 A_JumpIfNoAmmo("NoAmmo")
	 SNPF A 1
     SNPF B 1 A_FireBullets (3,4,1,20,"BulletSpark")
	 TNT1 A 0 A_SetPitch(pitch-1.5)
	 TNT1 A 0 A_SetAngle(angle+0.2)
	 TNT1 A 0 BRIGHT A_GunFlash
	 TNT1 A 0 A_AlertMonsters
     TNT1 A 0 A_FireCustomMissile("RifleCasingSpawner",0,0,0,0)
     SNPF CD 1
	 SNPG A 28
	 Goto Ready
   ScopedFire:
     TNT1 A 0 A_JumpIfNoAmmo("NoAmmo")
	 SNPS A 1 A_FireBullets (3,4,1,13,"BulletSpark")
	 TNT1 A 0 A_SetPitch(pitch-1.5)
	 TNT1 A 0 A_SetAngle(angle+0.2)
	 TNT1 A 0 BRIGHT A_GunFlash
	 TNT1 A 0 A_AlertMonsters
     TNT1 A 0 A_FireCustomMissile("RifleCasingSpawner",0,0,0,0)
	 SNPS A 28
	 Goto Ready
   AltFire:
     TNT1 A 0 A_JumpIfInventory("ScopeZoom", 2, "ZoomOut")
     TNT1 A 0 A_JumpIfInventory("ScopeZoom", 1, "ZoomMore")
     TNT1 A 0 A_PlaySound("sniper/scope")
     TNT1 A 0 A_GiveInventory("ScopeZoom", 1)
     SNPS A 2 A_ZoomFactor(4.0)
     Goto Ready
   ZoomMore:
     TNT1 A 0 A_PlaySound("sniper/scope")
     TNT1 A 0 A_GiveInventory("ScopeZoom", 1)
     SNPS A 2 A_ZoomFactor(8.0)
     Goto Ready
   ZoomOut:
     TNT1 A 0 A_TakeInventory("ScopeZoom", 2)
     TNT1 A 0 A_PlaySound("sniper/scope")
     SNPG A 2 A_ZoomFactor(1.0)
     Goto Ready
   Reload:
     TNT1 A 0 A_JumpIfInventory("ScopeZoom", 1, "ReloadScoped")
     TNT1 A 0 A_JumpIfInventory("ScopeZoom", 2, "ReloadScoped")
	 TNT1 A 0 A_JumpIfInventory("SniperRifleClip", 10, 2)
	 TNT1 A 0 A_JumpIfInventory("Clip", 1, "ReloadStart")
	 TNT1 A 0 A_JumpIfInventory("SniperRifleClip", 10, "Full")
	 TNT1 A 0
	 Goto ReloadStart
   ReloadScoped:
     TNT1 A 0 A_JumpIfInventory("SniperRifleClip", 10, 2)
	 TNT1 A 0 A_JumpIfInventory("Clip", 1, "ReloadStart")
	 TNT1 A 0 A_JumpIfInventory("SniperRifleClip", 10, "ScopedFull")
	 Goto ReloadStart
   ReloadStart:
     TNT1 A 0 A_TakeInventory("ScopeZoom", 2)
     TNT1 A 0 A_ZoomFactor(1.0)
	 SNPI ABCDEFGH 1
   ReloadWork:
	 SNPI H 5
	 TNT1 A 0 A_PlaySound ("sniper/clipout")
     SNPR AB 1
	 TNT1 A 0 A_FireCustomMissile("EmptyClipSpawner",0,0,0,0)
	 SNPR B 14
	 SNPR A 1
	 TNT1 A 0 A_PlaySound ("sniper/clipin")
	 SNPI H 14
	 TNT1 A 0 A_PlaySound ("sniper/cock")
   ReloadLoop:
	 TNT1 A 0 A_TakeInventory("Clip", 1)
	 TNT1 A 0 A_GiveInventory("SniperRifleClip",1)
	 TNT1 A 0 A_JumpIfInventory("SniperRifleClip", 10, "ReloadEnd")
	 TNT1 A 0 A_JumpIfInventory("Clip", 1, "ReloadLoop")
	 Goto ReloadEnd
   ReloadEnd:
	 SNPI HGFEDCBA 1
	 Goto Full
   Full:
     TNT1 A 0
     Goto Ready 
   ScopedFull:
     TNT1 A 0
     Goto ScopedReady 
   NoAmmo:
     TNT1 A 0 A_JumpIfInventory("SniperRifleClip", 10, 2)
	 TNT1 A 0 A_JumpIfInventory("Clip", 1, "Reload")
	 TNT1 A 0 A_PlaySound ("weapon/noammo")
     Goto Ready 
   Flash:
     TNT1 A 4 Bright A_Light1
     TNT1 A 3 Bright A_Light2
     Goto LightDone
   Spawn:
	 SNIP A -1
     Stop
   }
}
