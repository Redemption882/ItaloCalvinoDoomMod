ACTOR HiPointClip : Ammo
{
   +INVENTORY.IGNORESKILL
   Inventory.MaxAmount 15
}

ACTOR HiPointCarbine : Weapon
{
   inventory.pickupmessage "You got the Hi-Point 995 Carbine!"
   Inventory.PickupSound "weapon/pickup"
   Weapon.AmmoType1 "HiPointClip"
   Weapon.AmmoGive1 0
   Weapon.AmmoUse1 1
   Weapon.AmmoType2 "Clip"
   Weapon.AmmoGive2 20
   Weapon.Kickback 40
   Weapon.BobStyle InverseSmooth
   Weapon.BobSpeed 2.2
   Weapon.BobRangeY 0.3
   Weapon.BobRangeX 0.2
   Weapon.SelectionOrder 1303
   Weapon.SlotNumber 2
   Scale 0.1
   AttackSound "hipoint/fire"
   +NOAUTOFIRE
   +NOAUTOAIM
   +INVENTORY.IGNORESKILL
   +Weapon.Ammo_Optional 
   +weapon.wimpy_weapon
   +weapon.noalert
   States
   {
   Ready:
     HIPD A 1
     TNT1 A 0 A_WeaponReady(WRF_ALLOWRELOAD)
	 TNT1 A 0 A_WeaponOffset(CallACS("Script_GetGunOffsetX"), CallACS("Script_GetGunOffsetY"))
     Loop
   Deselect:
	 TNT1 A 0 
     Goto DeselectAnim
    DeselectAnim:
	 TNT1 A 0 A_PlaySound ("weapon/inventory")
	 HIPI ABCDEFGH 1
	 Goto DeselectEnd
   DeselectEnd:
	 TNT1 AAAAAAAAAAAAAA 00 A_Lower
     Loop
   Select:
	 TNT1 A 1 A_Raise
   	 TNT1 AAAAAAAAAAAAAA 0 A_Raise
     Goto SelectAnim	 
   SelectAnim:
	 TNT1 A 0 A_PlaySound ("weapon/inventory")
	 HIPI HGFEDCBA 1
	 Goto Ready
   Fire:
      TNT1 A 0 A_JumpIfNoAmmo("NoAmmo")
	  HIPF A 1
      HIPF B 1 A_FireBullets (4,3,-1,7,"BulletSpark")
	  TNT1 A 0 A_SetPitch(pitch-0.7)
	  TNT1 A 0 A_SetAngle(angle+0.5)
	  TNT1 A 0 BRIGHT A_GunFlash
	  TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_FireCustomMissile("PistolCasingSpawner",0,0,0,0)
      HIPF CDE 1
	  HIPD A 3
      Goto Ready
	Reload:
	  TNT1 A 0 A_JumpIfInventory("HiPointClip", 15, 2)
	  TNT1 A 0 A_JumpIfInventory("Clip", 1, "ReloadWork")
	  TNT1 A 0
	  Goto Ready
	ReloadWork:
	  TNT1 A 0 A_PlaySound ("hipoint/inv1")
	  HIPR ABCDEFGHI 1
	  HIPR I 12
	  TNT1 A 0 A_PlaySound ("hipoint/clipout")
	  HIPR JKL 1
	  TNT1 A 0 A_FireCustomMissile("EmptyClipSpawner",0,0,0,0)
	  HIPR L 16
	  TNT1 A 0 A_PlaySound ("hipoint/clipin")
	  HIPR KJI 1
	  HIPR I 6
	  TNT1 A 0 A_PlaySound ("hipoint/inv2")
	  HIPR HGFEDCBA 1
	ReloadLoop:
	  TNT1 A 0 A_TakeInventory("Clip", 1)
	  TNT1 A 0 A_GiveInventory("HiPointClip",1)
	  TNT1 A 0 A_JumpIfInventory("HiPointClip", 15, "Full")
	  TNT1 A 0 A_JumpIfInventory("Clip", 1, "ReloadLoop")
	Goto Full
	Full:
      TNT1 A 0
    Goto Ready 
	NoAmmo:
      TNT1 A 0 A_JumpIfInventory("HiPointClip", 15, 2)
	  TNT1 A 0 A_JumpIfInventory("Clip", 1, "ReloadWork")
	  TNT1 A 0 A_PlaySound ("weapon/noammo")
    Goto Ready 
	Flash:
      TNT1 A 4 Bright A_Light1
      TNT1 A 3 Bright A_Light2
    Goto LightDone
	Spawn:
	  HIPO A -1
      Stop
	}
}
