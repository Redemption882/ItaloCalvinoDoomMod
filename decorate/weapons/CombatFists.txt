actor CombatFists : Weapon Replaces Fist
{
  Weapon.SelectionOrder 1303
  Weapon.kickback 100
  Weapon.SlotNumber 1
  +NOAUTOFIRE
  +NOAUTOAIM
  +weapon.wimpy_weapon
  +weapon.meleeweapon
  +weapon.noalert
   Weapon.BobStyle InverseSmooth
   Weapon.BobSpeed 2.2
   Weapon.BobRangeY 0.3
   Weapon.BobRangeX 0.2
  States
  {
  Ready:
    PUND A 1
    TNT1 A 0 A_WeaponReady
	TNT1 A 0 A_WeaponOffset(CallACS("Script_GetGunOffsetX"), CallACS("Script_GetGunOffsetY"))
	TNT1 A 0 A_JumpIfInventory("GrenFire", 1, "GrenadeToss")
	Loop
  Ready2:
    PU2F D 1
	TNT1 A 0 A_JumpIfInventory("HoldFist", 1, "StrongPunch")
	Loop
  Ready3:
    TNT1 A 0 A_TakeInventory("StrongFist", 2)
    PUND A 1
	Goto Ready
  Deselect:
	TNT1 A 0
    Goto DeselectAnim
  DeselectAnim:
    PUND A 1
	TNT1 A 0 A_PlaySound ("weapon/inventory")
	PUNI ABCDEF 1
	Goto DeselectEnd
  DeselectEnd:
    TNT1 AAAAAAAAAAAAAA 00 A_Lower
    Loop
  Select:
    TNT1 A 1 
    TNT1 AAAAAAAAAAAAAA 00 A_Raise
    Goto SelectAnim
  SelectAnim:
    TNT1 A 0 A_PlaySound ("weapon/inventory")
    PUNI FEDCBA 1
	PUND A 1
	Goto Ready
  Fire:
    TNT1 A 0 A_JumpIfInventory("StrongFist", 2, "StrongFire")
	TNT1 A 0 A_GiveInventory("StrongFist", 1)
	PUNF AB 1
	PUNF CDEF 1
	TNT1 A 0 A_PlaySound("fists/swing")
	PUNF GHI 1
	PUNF M 1
	TNT1 A 0 A_CustomPunch(random(1,2),0,0,"FistPuff")
	PUNF M 3
	PUNF N 1
	PUNF O 1
	PUNF PQRS 1
	TNT1 A 0 A_Refire
	Goto Ready
  StrongFire:
    TNT1 A 0 A_TakeInventory("StrongFist", 2)
	PUNF AB 1
	PU3F CD 1
	TNT1 A 0 A_PlaySound("fists/swing")
	PU3F EG 1
	PU4F A 1
	TNT1 A 0 A_CustomPunch(random(2,3),0,0,"FistPuff")
	PU3F H 1
	PU4F B 1
	TNT1 A 0 A_CustomPunch(random(2,3),0,0,"FistPuff")
	PU3F I 1
	PU3F M 1
	TNT1 A 0 A_CustomPunch(random(2,3),0,0,"FistPuff")
	PU4F C 1
	PU3F N 1
	PU4F D 1
	PU3F O 1
	PU3F PQRS 1
	TNT1 A 0 A_Refire
	Goto Ready3
  AltFire:
    PU2F A 1
    PU2F B 1
	TNT1 A 0 A_GiveInventory("HoldFist", 1)
	PU2F C 1
	PU2F D 1
	TNT1 A 0 A_Refire
	Goto Ready2
  AltHold:
    PU2F D 1
	TNT1 A 0 A_GiveInventory("HoldFist", 1)
	TNT1 A 0 A_Refire
	Goto Ready2
  StrongPunch:
    TNT1 A 0
	TNT1 A 0 A_JumpIfInventory("HoldFist", 3, "AltFire_Strong1")
    Goto AltFire_Normal
  AltFire_Normal:
    TNT1 A 0 A_TakeInventory("HoldFist", 3)
	TNT1 A 0 A_GiveInventory("StrongFist", 1)
	PU2F A 1
	PU2F EF 1
	TNT1 A 0 A_PlaySound("fists/swing")
	PU2F GH 1
	TNT1 A 0 A_CustomPunch(random(6,8),0,0,"FistPuff")
	PU2F H 9
	PU2F I 2
	PU2F J 1
	PU2F KLM 1
	TNT1 A 0 A_Refire
	Goto Ready
  AltFire_Strong1:
    TNT1 A 0 A_TakeInventory("HoldFist", 3)
	TNT1 A 0 A_GiveInventory("StrongFist", 1)
    PU2F DCBA 1
	PU2F EF 1
	TNT1 A 0 A_PlaySound("fists/gswing", 4096)
	PU2F GH 1
	TNT1 A 0 A_CustomPunch(random(12,13),0,0,"FistPuff")
	TNT1 A 0 Radius_Quake (2, 5, 0, 5, 0)//(intensity, duration, damrad, tremrad, tid)
	PU2F H 11
	PU2F I 2
	PU2F J 1
	PU2F KLM 1
	Goto Ready
  }
}
